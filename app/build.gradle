buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'org.greenrobot:greendao-gradle-plugin:3.2.2'
    }
}

apply plugin: 'org.greenrobot.greendao'
apply plugin: 'com.android.application'

android {
    testOptions {
        unitTests.returnDefaultValues = true
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.xixicm.de"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'com.xixicm.de.MockTestRunner'
    }

    signingConfigs {
        Properties properties = new Properties()
        InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
        properties.load(inputStream)

        def debug_keyFile = properties.getProperty('debug.key.file')
        def release_keyFile = properties.getProperty('release.key.file')
        def key_keyAlias = properties.getProperty('keyAlias')
        def key_keyPassword = properties.getProperty('keyPassword')
        def key_storePassword = properties.getProperty('storePassword')

        debugConfig {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file(debug_keyFile)
            storePassword 'android'
        }
        releaseConfig {
            keyAlias key_keyAlias
            keyPassword key_keyPassword
            storeFile file(release_keyFile)
            storePassword key_storePassword
        }
    }
    buildTypes {
        debug {
            testCoverageEnabled true
            signingConfig signingConfigs.debugConfig
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.releaseConfig
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

greendao {
    schemaVersion 1
    daoPackage "com.xixicm.de.data.storage.dao"
    targetGenDir "src/main/java"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.xixicm.ca:ca:0.0.5'
    compile 'com.android.support:support-v4:25.0.1'
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.2.1'
    compile 'com.jakewharton:butterknife:8.2.1'
    compile 'org.greenrobot:greendao:3.2.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'dev.dworks.libs:volleyplus:0.1.4'

    // Dependencies for local unit tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile "org.mockito:mockito-core:1.10.19"

    // Dependencies for Android unit tests
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile "org.mockito:mockito-core:1.10.19"
    androidTestCompile 'com.crittercism.dexmaker:dexmaker:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-dx:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
    // Android Testing Support Library's runner and rules
    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support.test:rules:0.5"

    // Resolve conflicts between main and test APK:
    androidTestCompile "com.android.support:support-annotations:25.0.1"
    androidTestCompile "com.android.support:support-v4:25.0.1"
    androidTestCompile "com.android.support:recyclerview-v7:25.0.1"
    androidTestCompile 'com.android.support:design:25.0.1'
}
